(* precedence climbing boolean parsing *)
bool_and = bool_or, { "and", bool_or } ;
bool_or  = bool, { "or", bool } ;
bool = "true" | "false"
     | "!", bool
     | comparison ;
comparison = additive_expr, { "=" | ">" | ">=" | "<" | "<=" | "!=" , additive_expr } ;

(* precedence climbing parsing *)
additive_expr = multiplicative_expr, { ("+" | "-"), multiplicative_expr } ;
multiplicative_expr = parenthetical_multiplicative_expr, { ("*" | "/"), parenthetical_multiplicative_expr } ;
parenthetical_multiplicative_expr = power_expr, { "(", additive_expr, ")", { "^", factorial_expr } } ;
power_expr = factorial_expr, { "^", factorial_expr } ;
factorial_expr = factor, [ "!" ] ;
factor = "(", additive_expr, ")"
       | "|", additive_expr, "|"
       | function
       | "-", factor
       | constant
       | number
       | identifier, [ "=", additive_expr ] ;
function = identifier, factor ;
constant = "pi" | "e" ;

identifier = "A".."z", { "A".."z" } ;

digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
(* number = 52 or .14 or -65535 or -.256 or -340.430 etc *)
number = [ "-" ], ( digit, { digit }, [ ".", { digit } ] ) | ( ".", digit, { digit } ) ;
